pkgname=scribus
pkgver=1.5.3
pkgrel=3
pkgdesc="Desktop publishing software"
arch=('x86_64')
url="https://www.scribus.net/"
license=('GPL')
depends=('hunspell' 'libcdr' 'libmspub' 'libpagemaker' 'libvisio' 'podofo'
         'poppler' 'python2' 'qt5-base' 'hicolor-icon-theme' 'harfbuzz-icu')
makedepends=('cmake' 'boost' 'mesa' 'qt5-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        'findOpenSSL.patch'
        'fix-poppler-issue.patch'
        'fix-icu-0.61.1-compilation.patch')
sha256sums=('73a30b4727e19f5d301a936d23a84275cc4f5613a92416cbd843f5167721d74f'
            'd6d798a370442026e04d56769848761111d63af2ca69a6c2591233da3703dfb4'
            '9e9e954cb30fe606196cb8fc1864983fa571f3a729aec0c3ef01fc577be238a1'
            '9faa7fe6d5c7744511b5472ad0e376162360099b9456aaaf3ec78290b1bfef11')

prepare()
{
  msg "Preparing $pkgname..."
  cd ${pkgname}-${pkgver}

  patch -p1 -i ../findOpenSSL.patch
  patch -p1 -i ../fix-poppler-issue.patch
  patch -p1 -i ../fix-icu-0.61.1-compilation.patch

  sed \
      -e 's|#!/usr/bin/python|#!/usr/bin/python2|' \
      -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
      -i scribus/plugins/scriptplugin/{samples,scripts}/*

  mkdir -p build
}

build()
{
  msg "Building $pkgname..."
  cd ${pkgname}-${pkgver}/build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DCMAKE_SKIP_RPATH=ON
  make
}

package()
{
  msg "Packaging $pkgname..."
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
  cd ..

  msg2 "Install desktop entry..."
  install -Dm644 scribus.desktop -t $pkgdir/usr/share/applications

  for i in 16x16 32x32 128x128 256x256 512x512 1024x1024; do
    install -Dm644 resources/iconsets/artwork/icon_$i.png $pkgdir/usr/share/icons/hicolor/$i/apps/scribus.png
  done

  # Use system hyphen
  rm -rf $pkgdir/usr/share/scribus/dicts/hyph
  ln -sf /usr/share/hyphen $pkgdir/usr/share/scribus/dicts/hyph
}

# vim: ts=2:sw=2:sts=2
