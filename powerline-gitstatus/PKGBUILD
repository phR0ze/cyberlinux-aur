pkgname=powerline-gitstatus
pkgver=1.3.1
pkgrel=3
pkgdesc="A Powerline segment for showing the status of a Git working copy"
arch=('any')
url="https://github.com/phR0ze/powerline-gitstatus"
license=('MIT')
depends=('python' 'git' 'powerline')
makedepends=('python-setuptools')
install=powerline-gitstatus.install
source=("https://github.com/phR0ze/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
	colorschemes_default.json
	themes_shell_default.json)
sha256sums=(f198a6947dcd76cc380e308e10a56d0988b1b3d4c69517bf141f4daa483fe367
  fa349e0ebea95608f41db180778deed341117478fad4aa682783642b34be2f20
  b15c603a1b15c2b3f9dcfa77fd90422b1cce0db87b2bdea7c730453921d0dd59)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 README.md "${pkgdir}"/usr/share/doc/${pkgname}/README.md
  install -Dm644 "${srcdir}"/colorschemes_default.json "${pkgdir}"/usr/share/doc/${pkgname}/colorschemes/default.json
  install -Dm644 "${srcdir}"/themes_shell_default.json "${pkgdir}"/usr/share/doc/${pkgname}/themes/shell/default.json
}
