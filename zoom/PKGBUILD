# MIT License
# Copyright (c) 2017-2020 phR0ze
# 
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
# 
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
# 
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

#-------------------------------------------------------------------------------
# Maintained by phR0ze
# Adapted from Gordian Edenhofer <gordian.edenhofer@gmail.com> work
#-------------------------------------------------------------------------------
pkgname=zoom
pkgver=5.7.6a
_build=31792.0820
pkgrel=1
pkgdesc="Video Conferencing and Web Conferencing Service"
arch=('x86_64')
license=('custom')
url="https://zoom.us/"
makedepends=('imagemagick')
depends=('fontconfig' 'glib2' 'libpulse' 'libsm' 'ttf-font' 'libx11' 'libxtst' 'libxcb'
	'libxcomposite' 'libxfixes' 'libxi' 'libxcursor' 'libxkbcommon-x11' 'libxrandr'
	'libxrender' 'libxshmfence' 'libxslt' 'mesa' 'nss' 'xcb-util-image'
	'xcb-util-keysyms' 'dbus' 'ibus')
optdepends=('pulseaudio-alsa: audio via PulseAudio'
	'qt5-webengine: SSO login support'
	'picom: extra compositor needed by some window managers for screen sharing'
	'xcompmgr: extra compositor needed by some window managers for screen sharing')
options=(!strip)
source=("${pkgname}-${pkgver}_orig_x86_64.pkg.tar.xz"::"https://zoom.us/client/${pkgver:0:-2}${_build}/zoom_x86_64.pkg.tar.xz")
sha512sums=('7c34f2f1951a22391ced31960fedba4f1058abf55f1e530b1069bc9b2946f54cd3a12713135ab7ea0ae57131e2ce30521315ce4a67673f7f024cd23f6e03c4be')

build()
{
  msg "Building $pkgname..."
  sed -i 's/Icon=Zoom/Icon=zoom/g' "${srcdir}/usr/share/applications/Zoom.desktop"
  sed -i 's/StartupWMClass=Zoom/StartupWMClass=zoom/g' "${srcdir}/usr/share/applications/Zoom.desktop"
}

package()
{
  msg "Packaging $pkgname..."
  cp -dpr --no-preserve=ownership opt usr "${pkgdir}"

  msg2 "Installing app icons"
  install -dm 755 "$pkgdir"/usr/share/icons/hicolor/32x32/apps
  convert -background none -density 384 -resize x32 "$srcdir"/usr/share/pixmaps/Zoom.png "$pkgdir"/usr/share/icons/hicolor/32x32/apps/zoom.png

  install -dm 755 "$pkgdir"/usr/share/icons/hicolor/256x256/apps
  cp "$srcdir"/usr/share/pixmaps/Zoom.png "$pkgdir"/usr/share/icons/hicolor/256x256/apps/zoom.png
}

# vim: ts=2:sw=2:sts=2
